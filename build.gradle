buildscript {
  repositories {
    maven { url 'https://plugins.gradle.org/m2/' }
  }
  dependencies {
    classpath 'com.gradle.publish:plugin-publish-plugin:0.12.0'
  }
}

plugins {
  id 'java-gradle-plugin'
  id 'groovy'
  id 'maven-publish'
  id 'com.gradle.plugin-publish' version '0.12.0'
}

group 'com.github.ohmex.grdp'
version '1.0.0'

publishing {
  repositories {
    maven {
      url "../maven-repo"
    }
  }
}

pluginBundle {
  website = 'https://github.com/ohmex/grdp'
  vcsUrl = 'https://github.com/ohmex/grdp.git'
  tags = ['dependency', 'plugin', 'source']
}

gradlePlugin {
  plugins {
    gradletestPlugin {
      id = 'com.github.ohmex.grdp'
      displayName = 'Plugin for source dependency'
      description = 'A plugin that helps your app build with source as subproject'
      implementationClass = 'com.github.ohmex.grdp.SettingsPlugin'
    }
  }
}

//  repo for dependences resolution
repositories {
  jcenter()
}

// dependencies of this plugin
dependencies {
  implementation gradleApi()
  implementation localGroovy()

  implementation 'org.ajoberstar.grgit:grgit-core:4.0.2'
}

/* 
  Local Publish: publish to local maven repo for testing
  Use command : ./gradlew publish
   
  Usage in Target Project as below
  Inside : build.gradle
   
  plugins {
    id 'java'
    // import/apply your custom plugin
    id 'com.github.ohmex.grdp' version '1.0.0'
  }
  group 'com.github.ohmex.grdp'
  version '1.0.0'
  repositories {
    maven {
      url "../repos/maven-repo"
    }
    jcenter()
  }
 
  Publish the plugin to https://plugins.gradle.org/
  https://guides.gradle.org/publishing-plugins-to-gradle-plugin-portal/

  ./gradlew publishPlugins
  
  Consume plugin in settings.gradle
  plugins {
    id "com.github.ohmex.grdp" version "1.0.0"
  }

  or
  
  buildscript {
    repositories {
      maven {
        url "https://plugins.gradle.org/m2/"
      }
      jcenter()
    }
    dependencies {
      classpath 'com.github.ohmex.grdp:grdp:1.0.0'
    }
  }

  apply plugin: 'com.github.ohmex.grdp'
*/